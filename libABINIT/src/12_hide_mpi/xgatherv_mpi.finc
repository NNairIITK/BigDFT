!{\src2tex{textfont=tt}}
!!****f* ABINIT/xgatherv_mpi
!! NAME
!!  xgatherv_mpi
!!
!! FUNCTION
!!  This module contains functions that calls MPI routine,
!!  if we compile the code using the MPI CPP flags.
!!  xgatherv_mpi is the generic function.
!!
!! COPYRIGHT
!!  Copyright (C) 2001-2014 ABINIT group (MT,GG)
!!  This file is distributed under the terms of the
!!  GNU General Public License, see ~ABINIT/COPYING
!!  or http://www.gnu.org/copyleft/gpl.txt .
!!
!! SOURCE

!!***

!!****f* ABINIT/xgatherv_mpi_int
!! NAME
!!  xgatherv_mpi_int
!!
!! FUNCTION
!!  Gathers data from all tasks and delivers it to all.
!!  Target: one-dimensional integer arrays.
!!
!! INPUTS
!!  xval= buffer array
!!  recvcounts= number of received elements
!!  displs= relative offsets for incoming data
!!  nelem= number of elements
!!  root= rank of receiving process
!!  spaceComm= MPI communicator
!!
!! OUTPUT
!!  ier= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  recvbuf= received buffer
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_gatherv
!!
!! SOURCE
subroutine xgatherv_mpi_int(xval,nelem,recvbuf,recvcounts,displs,root,spaceComm,ier)

 use defs_basis

!This section has been created automatically by the script Abilint (TD).
!Do not modify the following lines by hand.
#undef ABI_FUNC
#define ABI_FUNC 'xgatherv_mpi_int'
!End of the abilint section

 implicit none

!Arguments-------------------------
 integer,intent(in) :: xval(:)
 integer,intent(inout)   :: recvbuf(:)
 integer,intent(in) :: recvcounts(:),displs(:)
 integer,intent(in) :: nelem,root,spaceComm
 integer,intent(out)   :: ier

!Local variables-------------------
 integer :: cc,dd

! *************************************************************************

 ier=0
#if defined HAVE_MPI
 if (spaceComm /= MPI_COMM_SELF .and. spaceComm /= MPI_COMM_NULL) then
   call MPI_gatherV(xval,nelem,MPI_INTEGER,recvbuf,recvcounts,displs,&
&   MPI_INTEGER,root,spaceComm,ier)
 else if (spaceComm == MPI_COMM_SELF) then
#endif
   dd=0;if (size(displs)>0) dd=displs(1)
   cc=size(xval);if (size(recvcounts)>0) cc=recvcounts(1)
   recvbuf(dd+1:dd+cc)=xval(1:cc)
#if defined HAVE_MPI
 end if
#endif
end subroutine xgatherv_mpi_int
!!***

!!****f* ABINIT/xgatherv_mpi_int1_dp1
!! NAME
!!  xgatherv_mpi_int1_dp1
!!
!! FUNCTION
!!  Gathers data from all tasks and delivers it to all.
!!  Target :  one-dimensional integer arrray and one-dimensionnal dp array
!!
!! INPUTS
!!  buf_int=buffer integer array that is going to be gathered
!!  buf_int_size=size of buf_int array
!!  buf_dp=buffer dp array that is going to be gathered
!!  buf_dp_size=size of buf_dp array
!!  spaceComm=MPI communicator to be gathered on it
!!  root=rank of receiving process
!!  spaceComm=MPI communicator
!!
!! OUTPUT
!!  buf_int_all=buffer integer array gathered
!!  buf_int_size_all=size of buffer integer array gathered
!!  buf_dp_all=buffer dp array gathered
!!  buf_dp_size_all=size of buffer dp array gathered
!!  ier=exit status, a non-zero value meaning there is an error
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_gatherv
!!
!! SOURCE

subroutine xgatherv_mpi_int1_dp1(buf_int,buf_int_size,buf_dp,buf_dp_size, &
&          buf_int_all,buf_int_size_all,buf_dp_all,buf_dp_size_all,root,&
&          spaceComm,ier)

 use defs_basis

!This section has been created automatically by the script Abilint (TD).
!Do not modify the following lines by hand.
#undef ABI_FUNC
#define ABI_FUNC 'xgatherv_mpi_int1_dp1'
!End of the abilint section

 implicit none

!Arguments-------------------------
!scalars
 integer,intent(in) :: buf_int_size,buf_dp_size,root,spaceComm
 integer,intent(out) :: buf_int_size_all,buf_dp_size_all,ier
!arrays
 integer,intent(in) :: buf_int(:)
 integer,allocatable,target,intent(out) :: buf_int_all(:)
 real(dp),intent(in) :: buf_dp(:)
 real(dp),allocatable,target, intent(out) :: buf_dp_all(:)

!Local variables--------------
!scalars
 integer :: buf_pack_size,ierr,ii,iproc,istart_dp,istart_int
 integer :: lg,lg1,lg2,lg_int,lg_dp,me,n1,nproc,position
 integer :: totalbufcount
 logical,parameter :: use_pack=.false.
!arrays
 integer :: buf_size(2),pos(3)
 integer,allocatable :: buf_dp_size1(:),buf_int_size1(:)
 integer,allocatable :: count_dp(:),count_int(:),count_size(:),counts(:)
 integer,allocatable :: disp_dp(:),disp_int(:),displ(:),displ_dp(:),displ_int(:)
 integer,allocatable :: pos_all(:)
 integer,pointer:: outbuf_int(:)
 real(dp ) :: tsec(2)
 real(dp),pointer :: outbuf_dp(:)
 character,allocatable :: buf_pack(:),buf_pack_tot(:)

! *************************************************************************

 ier=0

#if defined HAVE_MPI
 if (spaceComm/=MPI_COMM_SELF.and.spaceComm/=MPI_COMM_NULL) then

   nproc=xcomm_size(spaceComm)

!First version: using 2 allgather (one for ints, another for reals)
!------------------------------------------------------------------
   if (.not.use_pack) then

!  Prepare communications
     ABI_ALLOCATE(count_int,(nproc))
     ABI_ALLOCATE(disp_int,(nproc))
     ABI_ALLOCATE(count_dp,(nproc))
     ABI_ALLOCATE(disp_dp,(nproc))
     ABI_ALLOCATE(count_size,(2*nproc))
     buf_size(1)=buf_int_size;buf_size(2)=buf_dp_size
     call xallgather_mpi(buf_size,2, count_size,spaceComm,ier)
     do iproc=1,nproc
       count_int(iproc)=count_size(2*iproc-1)
       count_dp(iproc)=count_size(2*iproc)
     end do
     disp_int(1)=0;disp_dp(1)=0
     do ii=2,nproc
       disp_int(ii)=disp_int(ii-1)+count_int(ii-1)
       disp_dp (ii)=disp_dp (ii-1)+count_dp (ii-1)
     end do
     buf_int_size_all=sum(count_int)
     buf_dp_size_all =sum(count_dp)
     ABI_ALLOCATE(buf_int_all,(buf_int_size_all))
     ABI_ALLOCATE(buf_dp_all ,(buf_dp_size_all))

!  Communicate (one call for integers, one call for reals)
     call xgatherv_mpi(buf_int,buf_int_size,buf_int_all,count_int,disp_int,root,spaceComm,ierr)
     call xgatherv_mpi(buf_dp,buf_dp_size,buf_dp_all,count_dp,disp_dp,root,spaceComm,ierr)

!  Release the memory
     ABI_DEALLOCATE(count_int)
     ABI_DEALLOCATE(disp_int)
     ABI_DEALLOCATE(count_dp)
     ABI_DEALLOCATE(disp_dp)
     ABI_DEALLOCATE(count_size)

!2nd version: using 1 allgather (with MPI_PACK)
!-----------------------------------------------------------------
   else

     me=xcomm_rank(spaceComm)

!  Compute size of message
     call MPI_PACK_SIZE(buf_int_size,MPI_INTEGER,spaceComm,lg1,ier)
     call MPI_PACK_SIZE(buf_dp_size,MPI_DOUBLE_PRECISION,spaceComm,lg2,ier)
     lg=lg1+lg2

!  Pack data to be sent
     position=0;buf_pack_size=lg1+lg2
     ABI_ALLOCATE(buf_pack,(buf_pack_size))
     call MPI_PACK(buf_int,buf_int_size,MPI_INTEGER,buf_pack,buf_pack_size,position,spaceComm,ier)
     call MPI_PACK(buf_dp,buf_dp_size,MPI_DOUBLE_PRECISION,buf_pack,buf_pack_size,position,spaceComm,ier)

!  Gather size of all packed messages
     ABI_ALLOCATE(pos_all,(nproc*3))
     ABI_ALLOCATE(counts,(nproc))
     ABI_ALLOCATE(buf_int_size1,(nproc))
     ABI_ALLOCATE(buf_dp_size1,(nproc))
     ABI_ALLOCATE(displ,(nproc))
     ABI_ALLOCATE(displ_int,(nproc))
     ABI_ALLOCATE(displ_dp,(nproc))
     pos(1)=position;pos(2)=buf_int_size;pos(3)=buf_dp_size
     call MPI_ALLGATHER(pos,3,MPI_INTEGER,pos_all,3,MPI_INTEGER,spaceComm,ier)
     ii=1
     do iproc=1,nproc
       counts(iproc)=pos_all(ii);ii=ii+1
       buf_int_size1(iproc)=pos_all(ii);ii=ii+1
       buf_dp_size1(iproc)=pos_all(ii);ii=ii+1
     end do

     displ(1)=0 ; displ_int(1)=0 ; displ_dp(1)=0
     do iproc=2,nproc
       displ(iproc)=displ(iproc-1)+counts(iproc-1)
       displ_int(iproc)=displ_int(iproc-1)+buf_int_size1(iproc-1)
       displ_dp(iproc)=displ_dp(iproc-1)+buf_dp_size1(iproc-1)
     end do

     totalbufcount=displ(nproc)+counts(nproc)
     ABI_ALLOCATE(buf_pack_tot,(totalbufcount))
     buf_int_size_all=sum(buf_int_size1)
     buf_dp_size_all=sum(buf_dp_size1)

     if (me==root) then
       ABI_ALLOCATE(buf_int_all,(buf_int_size_all))
       ABI_ALLOCATE(buf_dp_all,(buf_dp_size_all))
     else
       ABI_ALLOCATE(buf_int_all,(1))
       ABI_ALLOCATE(buf_dp_all,(1))
     end if

!  Gather all packed messages
     call MPI_GATHERV(buf_pack,position,MPI_PACKED,buf_pack_tot,counts,displ,MPI_PACKED,root,spaceComm,ier)
     if (me==root) then
       position=0
       do iproc=1,nproc
         lg_int=buf_int_size1(iproc); lg_dp=buf_dp_size1(iproc)
         istart_int=displ_int(iproc); istart_dp=displ_dp(iproc)
         outbuf_int=>buf_int_all(istart_int+1:istart_int+lg_int)
         call MPI_UNPACK(buf_pack_tot,totalbufcount,position, outbuf_int, &
&         lg_int, MPI_INTEGER,spaceComm,ier)
         outbuf_dp=>buf_dp_all(istart_dp+1:istart_dp+lg_dp)
         call MPI_UNPACK(buf_pack_tot,totalbufcount,position,outbuf_dp, &
&         lg_dp, MPI_DOUBLE_PRECISION,spaceComm,ier)
       end do
     end if

!  Release the memory
     ABI_DEALLOCATE(pos_all)
     ABI_DEALLOCATE(counts)
     ABI_DEALLOCATE(buf_int_size1)
     ABI_DEALLOCATE(buf_dp_size1)
     ABI_DEALLOCATE(displ)
     ABI_DEALLOCATE(displ_int)
     ABI_DEALLOCATE(displ_dp)
     ABI_DEALLOCATE(buf_pack_tot)
     ABI_DEALLOCATE(buf_pack)

   end if
 else if (spaceComm == MPI_COMM_SELF) then
#endif

!Sequential version
   ABI_ALLOCATE(buf_int_all,(buf_int_size))
   ABI_ALLOCATE(buf_dp_all,(buf_dp_size))
   buf_int_all(:)=buf_int(:)
   buf_dp_all(:)=buf_dp(:)
   buf_int_size_all=buf_int_size
   buf_dp_size_all=buf_dp_size

#if defined HAVE_MPI
 end if
#endif

end subroutine xgatherv_mpi_int1_dp1
!!***

!!****f* ABINIT/xgatherv_mpi_int2d
!! NAME
!!  xgatherv_mpi_int2d
!!
!! FUNCTION
!!  This module contains functions that calls MPI routine,
!!  if we compile the code using the MPI CPP flags.
!!  xgatherv_mpi is the generic function.
!!
!! INPUTS
!!  xval= buffer array
!!  recvcounts= number of received elements
!!  displs= relative offsets for incoming data
!!  nelem= number of elements
!!  root= rank of receiving process
!!  spaceComm= MPI communicator
!!
!! OUTPUT
!!  ier= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  recvbuf= received buffer
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_gatherv
!!
!! SOURCE

subroutine xgatherv_mpi_int2d(xval,nelem,recvbuf,recvcounts,displs,root,spaceComm,ier)

 use defs_basis

!This section has been created automatically by the script Abilint (TD).
!Do not modify the following lines by hand.
#undef ABI_FUNC
#define ABI_FUNC 'xgatherv_mpi_int2d'
!End of the abilint section

 implicit none

!Arguments-------------------------
 integer,intent(in) :: xval(:,:)
 integer,intent(inout) :: recvbuf(:,:)
 integer,intent(in) :: recvcounts(:),displs(:)
 integer,intent(in) :: nelem,root,spaceComm
 integer,intent(out)   :: ier

!Local variables--------------
 integer :: cc,dd,sz1

! *************************************************************************

 ier=0
#if defined HAVE_MPI
 if (spaceComm /= MPI_COMM_SELF .and. spaceComm /= MPI_COMM_NULL) then
   call MPI_gatherV(xval,nelem,MPI_INTEGER,recvbuf,recvcounts,displs,&
&   MPI_INTEGER,root,spaceComm,ier)
 else if (spaceComm == MPI_COMM_SELF) then
#endif
   sz1=size(xval,1)
   dd=0;if (size(displs)>0) dd=displs(1)/sz1
   cc=size(xval,2);if (size(recvcounts)>0) cc=recvcounts(1)/sz1
   recvbuf(:,dd+1:dd+cc)=xval(:,1:cc)
#if defined HAVE_MPI
 end if
#endif
end subroutine xgatherv_mpi_int2d
!!***

!!****f* ABINIT/xgatherv_mpi_dp
!! NAME
!!  xgatherv_mpi_dp
!!
!! FUNCTION
!!  Gathers data from all tasks and delivers it to all.
!!  Target: one-dimensional double precision arrays.
!!
!! INPUTS
!!  xval= buffer array
!!  recvcounts= number of received elements
!!  displs= relative offsets for incoming data
!!  nelem= number of elements
!!  root= rank of receiving process
!!  spaceComm= MPI communicator
!!
!! OUTPUT
!!  ier= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  recvbuf= received buffer
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_gatherv
!!
!! SOURCE
subroutine xgatherv_mpi_dp(xval,nelem,recvbuf,recvcounts,displs,root,spaceComm,ier)

 use defs_basis

!This section has been created automatically by the script Abilint (TD).
!Do not modify the following lines by hand.
#undef ABI_FUNC
#define ABI_FUNC 'xgatherv_mpi_dp'
!End of the abilint section

 implicit none


!Arguments-------------------------
 real(dp),intent(in) :: xval(:)
 real(dp),intent(inout)   :: recvbuf(:)
 integer,intent(in) :: recvcounts(:),displs(:)
 integer,intent(in) :: nelem,root,spaceComm
 integer,intent(out)   :: ier

!Local variables--------------
 integer :: cc,dd

! *************************************************************************

 ier=0
#if defined HAVE_MPI
 if (spaceComm /= MPI_COMM_SELF .and. spaceComm /= MPI_COMM_NULL) then
   call MPI_gatherV(xval,nelem,MPI_DOUBLE_PRECISION,recvbuf,recvcounts,displs,&
&   MPI_DOUBLE_PRECISION,root,spaceComm,ier)
 else if (spaceComm == MPI_COMM_SELF) then
#endif
   dd=0;if (size(displs)>0) dd=displs(1)
   cc=size(xval);if (size(recvcounts)>0) cc=recvcounts(1)
   recvbuf(dd+1:dd+cc)=xval(1:cc)
#if defined HAVE_MPI
 end if
#endif
end subroutine xgatherv_mpi_dp
!!***

!!****f* ABINIT/xgatherv_mpi_dp2d
!! NAME
!!  xgatherv_mpi_dp2d
!!
!! FUNCTION
!!  Gathers data from all tasks and delivers it to all.
!!  Target: double precision two-dimensional arrays.
!!
!! INPUTS
!!  xval= buffer array
!!  recvcounts= number of received elements
!!  displs= relative offsets for incoming data
!!  nelem= number of elements
!!  root= rank of receiving process
!!  spaceComm= MPI communicator
!!
!! OUTPUT
!!  ier= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  recvbuf= received buffer
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_gatherv
!!
!! SOURCE
subroutine xgatherv_mpi_dp2d(xval,nelem,recvbuf,recvcounts,displs,root,spaceComm,ier)

 use defs_basis

!This section has been created automatically by the script Abilint (TD).
!Do not modify the following lines by hand.
#undef ABI_FUNC
#define ABI_FUNC 'xgatherv_mpi_dp2d'
!End of the abilint section

 implicit none

!Arguments-------------------------
 real(dp),intent(in) :: xval(:,:)
 real(dp),intent(inout) :: recvbuf(:,:)
 integer,intent(in) :: recvcounts(:),displs(:)
 integer,intent(in) :: nelem,root,spaceComm
 integer,intent(out)   :: ier

!Local variables--------------
 integer :: cc,dd,sz1

! *************************************************************************

 ier=0
#if defined HAVE_MPI
 if (spaceComm /= MPI_COMM_SELF .and. spaceComm /= MPI_COMM_NULL) then
   call MPI_gatherV(xval,nelem,MPI_DOUBLE_PRECISION,recvbuf,recvcounts,displs,&
&   MPI_DOUBLE_PRECISION,root,spaceComm,ier)
 else if (spaceComm == MPI_COMM_SELF) then
#endif
   sz1=size(xval,1)
   dd=0;if (size(displs)>0) dd=displs(1)/sz1
   cc=size(xval,2);if (size(recvcounts)>0) cc=recvcounts(1)/sz1
   recvbuf(:,dd+1:dd+cc)=xval(:,1:cc)
#if defined HAVE_MPI
 end if
#endif
end subroutine xgatherv_mpi_dp2d
!!***

!!****f* ABINIT/xgatherv_mpi_dp3d
!! NAME
!!  xgatherv_mpi_dp3d
!!
!! FUNCTION
!!  Gathers data from all tasks and delivers it to all.
!!  Target: double precision three-dimensional arrays.
!!
!! INPUTS
!!  xval= buffer array
!!  recvcounts= number of received elements
!!  displs= relative offsets for incoming data
!!  nelem= number of elements
!!  root= rank of receiving process
!!  spaceComm= MPI communicator
!!
!! OUTPUT
!!  ier= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  recvbuf= received buffer
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_gatherv
!!
!! SOURCE
subroutine xgatherv_mpi_dp3d(xval,nelem,recvbuf,recvcounts,displs,root,spaceComm,ier)

 use defs_basis

!This section has been created automatically by the script Abilint (TD).
!Do not modify the following lines by hand.
#undef ABI_FUNC
#define ABI_FUNC 'xgatherv_mpi_dp3d'
!End of the abilint section

 implicit none

!Arguments-------------------------
 real(dp),intent(in) :: xval(:,:,:)
 real(dp),intent(inout)   :: recvbuf(:,:,:)
 integer,intent(in) :: recvcounts(:),displs(:)
 integer,intent(in) :: nelem,root,spaceComm
 integer,intent(out)   :: ier

!Local variables--------------
 integer :: cc,dd,sz12

! *************************************************************************

 ier=0
#if defined HAVE_MPI
 if (spaceComm /= MPI_COMM_SELF .and. spaceComm /= MPI_COMM_NULL) then
   call MPI_gatherV(xval,nelem,MPI_DOUBLE_PRECISION,recvbuf,recvcounts,displs,&
&   MPI_DOUBLE_PRECISION,root,spaceComm,ier)
 else if (spaceComm == MPI_COMM_SELF) then
#endif
   sz12=size(xval,1)*size(xval,2)
   dd=0;if (size(displs)>0) dd=displs(1)/sz12
   cc=size(xval,3);if (size(recvcounts)>0) cc=recvcounts(1)/sz12
   recvbuf(:,:,dd+1:dd+cc)=xval(:,:,1:cc)
#if defined HAVE_MPI
 end if
#endif
end subroutine xgatherv_mpi_dp3d
!!***

!!****f* ABINIT/xgatherv_mpi_dp4d
!! NAME
!!  xgatherv_mpi_dp4d
!!
!! FUNCTION
!!  Gathers data from all tasks and delivers it to all.
!!  Target: double precision four-dimensional arrays.
!!
!! INPUTS
!!  xval= buffer array
!!  recvcounts= number of received elements
!!  displs= relative offsets for incoming data
!!  nelem= number of elements
!!  root= rank of receiving process
!!  spaceComm= MPI communicator
!!
!! OUTPUT
!!  ier= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  recvbuf= received buffer
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_gatherv
!!
!! SOURCE
subroutine xgatherv_mpi_dp4d(xval,nelem,recvbuf,recvcounts,displs,root,spaceComm,ier)

 use defs_basis

!This section has been created automatically by the script Abilint (TD).
!Do not modify the following lines by hand.
#undef ABI_FUNC
#define ABI_FUNC 'xgatherv_mpi_dp4d'
!End of the abilint section

 implicit none

!Arguments-------------------------
 real(dp),intent(in) :: xval(:,:,:,:)
 real(dp),intent(inout)   :: recvbuf(:,:,:,:)
 integer,intent(in) :: recvcounts(:),displs(:)
 integer,intent(in) :: nelem,root,spaceComm
 integer,intent(out)   :: ier

!Local variables-------------------
 integer :: cc,dd,sz123

! *************************************************************************

 ier=0
#if defined HAVE_MPI
 if (spaceComm /= MPI_COMM_SELF .and. spaceComm /= MPI_COMM_NULL) then
   call MPI_gatherV(xval,nelem,MPI_DOUBLE_PRECISION,recvbuf,recvcounts,displs,&
&   MPI_DOUBLE_PRECISION,root,spaceComm,ier)
 else if (spaceComm == MPI_COMM_SELF) then
#endif
   sz123=size(xval,1)*size(xval,2)*size(xval,3)
   dd=0;if (size(displs)>0) dd=displs(1)/sz123
   cc=size(xval,4);if (size(recvcounts)>0) cc=recvcounts(1)/sz123
   recvbuf(:,:,:,dd+1:dd+cc)=xval(:,:,:,1:cc)
#if defined HAVE_MPI
 end if
#endif
end subroutine xgatherv_mpi_dp4d
!!***
